{
  parserClass="com.gman.idea.plugin.concordion.lang.ConcordionParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Concordion"
  psiImplClassSuffix="Impl"
  psiPackage="com.gman.idea.plugin.concordion.lang.psi"
  psiImplPackage="com.gman.idea.plugin.concordion.lang.psi"

  elementTypeHolderClass="com.gman.idea.plugin.concordion.lang.psi.ConcordionTypes"
  elementTypeClass="com.gman.idea.plugin.concordion.lang.ConcordionElementType"
  tokenTypeClass="com.gman.idea.plugin.concordion.lang.ConcordionTokenType"

  tokens = [
    LPARENTH="("
    RPARENTH=")"
    LBRACKET="["
    RBRACKET="]"
    DOT="."
    COMA=","
    HASH="#"
    EQ="="
    COLON=":"
    SEMICOLON=";"
  ]
}

file ::= setExpression|iterateExpression|ognlExpressionStart;

setExpression ::= variable '=' ognlExpressionStart
iterateExpression ::= variable ':' ognlExpressionStart

ognlExpressionStart ::= (literal|method|field|variable) index* ('.' ognlExpressionNext)?
ognlExpressionNext ::= (method|field) index* ('.' ognlExpressionNext)*

literal ::= (STRING_LITERAL|DOUBLE_LITERAL|INTEGER_LITERAL)

method ::= IDENTIFIER '(' arguments ')' {
  implements="com.gman.idea.plugin.concordion.lang.psi.ConcordionMethodInternal"
  extends="com.gman.idea.plugin.concordion.lang.psi.ConcordionMethodInternalImpl"
}
field ::= IDENTIFIER {
  implements="com.gman.idea.plugin.concordion.lang.psi.ConcordionFieldInternal"
  extends="com.gman.idea.plugin.concordion.lang.psi.ConcordionFieldInternalImpl"

}
variable ::= '#' IDENTIFIER {
  implements="com.gman.idea.plugin.concordion.lang.psi.ConcordionVariableInternal"
  extends="com.gman.idea.plugin.concordion.lang.psi.ConcordionVariableInternalImpl"
}

arguments ::= ognlExpressionStart? (',' ognlExpressionStart)*
index ::= '[' ognlExpressionStart ']'