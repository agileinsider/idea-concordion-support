plugins {
    id 'org.jetbrains.intellij' version '0.0.42'
    id 'net.saliman.cobertura' version '2.2.8'
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'net.saliman.cobertura'

group 'org.concordion.plugin.idea'
version '0.5.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '2.10'
}

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    pluginName 'IdeaConcordionSupport'
    plugins 'junit'
    version '15.0.4'
//    sandboxDirectory "$projectDir/idea-sandbox"
    downloadSources false
    updateSinceUntilBuild false
}

ext {
    markdownSupportPlugin = files(
            "${projectDir}/markdown/lib/markdown.jar",
            "${projectDir}/markdown/lib/markdown-javafx-preview.jar",
            "${projectDir}/markdown/lib/intellij-markdown.jar",
            "${projectDir}/markdown/lib/Loboevolution.jar"
    )
}

configurations {
    compileWith
}
sourceSets {
    main.compileClasspath += configurations.compileWith
    test.compileClasspath += configurations.compileWith

    main.java.srcDirs = ['gen', 'src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['tests']

    testMd.java.srcDirs = ['tests', 'testsMd']
    testMd.compileClasspath += sourceSets.main.compileClasspath
    testMd.compileClasspath += sourceSets.test.compileClasspath
}

dependencies {
    compileWith markdownSupportPlugin

    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

task testMarkdownSupportIntegration(type: Test) {
    classpath += markdownSupportPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDir = sourceSets.testMd.output.classesDir
}

check.dependsOn testMarkdownSupportIntegration